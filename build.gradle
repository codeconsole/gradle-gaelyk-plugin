apply plugin: 'groovy'
//apply plugin: 'code-quality'

apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.5
targetCompatibility = 1.5
version = '0.1'

codeNarcConfigFileName = 'config/codenarc/rules.groovy'

repositories {
    mavenCentral()
	mavenCentral name:'jgit-repository', urls:'http://download.eclipse.org/jgit/maven'
	mavenRepo urls: "http://m2repo.spockframework.org/snapshots"
    add(new org.apache.ivy.plugins.resolver.URLResolver()) {
        name = "GitHub"
    	addArtifactPattern 'http://cloud.github.com/downloads/glaforge/[organisation]/[module]-[revision].[ext]'
    }
}

dependencies {
    groovy fileTree(dir: new File(gradle.gradleHomeDir, 'lib'), includes: ['**/groovy-all-*.jar'])
    
	compile gradleApi()
    compile group: 'gaelyk', name: 'gaelyk', version: '0.6.1'
	
	compile 'org.eclipse.jgit:org.eclipse.jgit:0.10.1'
	compile 'org.eclipse.jgit:org.eclipse.jgit.ui:0.10.1'
	compile 'com.jcraft:jsch:0.1.44-1'
	compile 'org.eclipse.jgit:org.eclipse.jgit.pgm:0.10.1'
	compile 'args4j:args4j:2.0.12'
    	
	testCompile "org.spockframework:spock-core:0.5-groovy-1.7"
    testCompile "junit:junit:4.+"
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Gaelyk plugin',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty("user.name"),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty("java.version")
    }
}

ideaProject {
    javaVersion = '1.6'
  
    withXml { provider ->
        def node = provider.asNode()

        // Use GIT
        def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
        vcsConfig.mapping[0].'@vcs' = 'Git'

        // Set Gradle home
        def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
        gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
    }
}
